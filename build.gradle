buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
	}
}


ext {
	verGwt = '2.8.0-beta1'

	dockerDir = "$projectDir/docker"
}

apply plugin: 'java'
apply plugin: 'gwt'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
	gradleVersion = '2.11'
}

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
	compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
	compile group: 'com.google.inject', name: 'guice', version: '4.0'
	compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: '4.0'
	compile group: 'com.squarespace.jersey2-guice', name: 'jersey2-guice', version: '0.10'
	compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.19'
	compile group: 'com.owlike', name: 'genson', version: '1.3'
	compile group: 'org.mybatis', name: 'mybatis', version: '3.3.1'
	compile group: 'org.mybatis', name: 'mybatis-guice', version: '3.7.1'
	compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.8.11.2'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
	compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.7'
	compile group: 'log4j', name: 'log4j', version: '1.2.17'

	gwt group: 'com.allen-sauer.gwt.log', name: 'gwt-log', version: '3.3.2'
	gwt group: 'com.allen-sauer.gwt.dnd', name: 'gwt-dnd', version: '3.3.4'
	gwt (group: 'com.google.gwt.inject', name: 'gin', version: '2.1.2') {
			exclude(module: 'guice')
			exclude(module: 'guice-assistedinject')
	}

	testCompile group: 'junit', name: 'junit', version: '4.11'
	testCompile group: 'com.google.gwt', name: 'gwt-dev', version: verGwt
	//testCompile group: 'com.google.gwt.gwtmockito', name: 'gwtmockito', version: '1.1.5'
	//testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'
	//testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

sourceSets {
	main {
		resources {
			srcDir 'src/main/resources'
			srcDir 'src/main/webapp'
		}
	}
}

gwt {
	gwtVersion = "${verGwt}"
	System.out.println("Using GWT version " + gwtVersion)

	minHeapSize = "512M";
	maxHeapSize = "2048M";

	modules 'se.umu.cs.ldbn.Ldbn'

	logLevel = 'INFO'

	// define the local workers for GWT, we assume at least dual core, but exclude any possible HT fake CPU cores
	def gwtLocalWorkers = Math.max(2, (int) (Runtime.getRuntime().availableProcessors() / 2))
	System.out.println("Using " + gwtLocalWorkers + " GWT compiler workers.")

	// for compiler reference see
	// http://steffenschaefer.github.io/gwt-gradle-plugin/doc/latest/javadoc/de/richsource/gradle/plugins/gwt/GwtCompileOptions.html
	compiler {
		strict = true;
		disableClassMetadata = true;
		disableCastChecking = true;
		localWorkers = gwtLocalWorkers;
	}
}

war {
	//dependsOn "gitRevision"

	archiveName = "ldbn.war"

	excludes += [
			"**/rebel.xml", "**/*.sql", "**/*.php"
	]

	//from "${buildDir}/classes/main"
	
}

war.doLast {
	File explodedWarDir = "${buildDir}/libs/ldbn" as File
	ant.unzip(src: war.archivePath, dest: explodedWarDir)
}

task copyWarToDocker(type: Copy, dependsOn: 'jar') {
	description 'Copies the war file to the target docker directory.'
	from war.archivePath
	into dockerDir
}

assemble.dependsOn copyWarToDocker


task gitRevision(type: Exec) {
	commandLine "git", "rev-parse", "--short", "HEAD"
}


jettyRunWar.contextPath = ''


// put GWT SDK and GWT client libraries on class path using provided scope
idea {
	module {
		scopes.PROVIDED.plus += [configurations.gwtSdk, configurations.gwt]
	}
}

eclipse {
	classpath {
		plusConfigurations += [configurations.gwtSdk, configurations.gwt]
	}
}